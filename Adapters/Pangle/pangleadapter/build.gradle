import groovy.json.JsonSlurper

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

def versionsFile = file("../versions.json")
def versionsData = new JsonSlurper().parse(versionsFile)

ext {
    mediationsdkVersion = versionsData.mediationsdk
    adapterVersion = versionsData.adapter
    sdkVersion = versionsData.sdk
}

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}


repositories {
    mavenCentral()
    maven { 
        url 'https://artifact.bytedance.com/repository/pangle'
    }
}

android {
    compileSdkVersion 34
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 34
        versionCode 1
        versionName adapterVersion
        buildConfigField "String", "GitHash", "\"${getGitHash()}\""
        buildConfigField("String","VERSION_NAME","\"${defaultConfig.versionName}\"")
        multiDexEnabled true
    }
    
    buildTypes {
        debug{
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/**'
    }
}

 dependencies {
     implementation "com.unity3d.ads-mediation:mediation-sdk:${mediationsdkVersion}"
     implementation "com.pangle.global:ads-sdk:${sdkVersion}"
     implementation "com.google.android.gms:play-services-ads-identifier:17.1.0"
     implementation "androidx.multidex:multidex:2.0.1"
 }

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from android.sourceSets.main.java.srcDirs
    exclude 'META-INF.com.android.build.gradle.aar-metadata.properties'
}

task createAAR(type: Copy,group:'buildTasks') {

    dependsOn (":${project.name}:assembleRelease")
    from "build/outputs/aar"
    into '../ReleaseCandidates/'
    include '*.aar'
    rename { String fileName ->
        fileName.replace('release', adapterVersion)
    }
}


